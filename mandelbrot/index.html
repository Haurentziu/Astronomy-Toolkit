<!DOCTYPE html>
<html>
  <head>

    <title>Mandelbrot Set</title>
    <link rel="stylesheet" type="text/css" href="../res/threeStylesheet.css" />
    <script type="text/javascript" src="../lib/analytics.js"></script>
  </head>
  <body>

  <script id="fractal-vertex" type="x-shader/x-vertex">
         precision highp float;
         uniform float zoom;
         uniform float aspect_ratio;
         uniform vec2 origin;
         varying vec2 pos;
         void main () {
             pos = origin + vec2(position.x * aspect_ratio, position.y) / zoom;
             gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
         }
     </script>

    <script id="fractal-fragment" type="x-shader/x-fragment">
      precision highp float;
      varying vec2 pos;
      uniform int coloring;
      uniform float color_density;
      uniform float color_offset;

      vec3 hsv2rgb(vec3 c){
        vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
        vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
        return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
      }

      float catmullRom(float sum, float sum1, float sum2, float sum3, float d){
        float d_2 = d * d;
        float d_3 = d_2 * d;
    		float cr = 0.5*(d_3 - d_2) * sum;
    		cr += 0.5 * (4.0* d_2 + d - 3.0 * d_3) * sum1;
    	  cr += 0.5 * (2.0 - 5.0 * d_2 + 3.0 * d_3) * sum2;
    		cr += 0.5 * (2.0 * d_2 - d - d_3) * sum3 + 1.0;
    		return cr;
      //  return 0.0;
      }

        vec2 squareVector(vec2 z){
          vec2 z_square = vec2(z.x * z.x - z.y * z.y, 2.0 * z.x * z.y);
          return z_square;
        }

      void main () {
        vec2 z = pos;
        float max_abs_sq = 1e7;
        bool hasEscaped = false;
        float max_iter = 400.0;

        bool isAverageColoring = (coloring == 4) || (coloring == 5);

        float sum = 0.0, sum1 = 0.0, sum2 = 0.0, sum3 = 0.0;

        float color = 0.0;
        float completed_iterations = max_iter;

        for(float iter = 0.0; iter < 400.0; iter++){
          vec2 z_power =  squareVector(z);//z^p
          z = pos + z_power;

        if(coloring == 4){
            sum3 = sum2;
            sum2 = sum1;
            sum1 = sum;

            if(iter > 1.0){
              float abs_last_z = length(z_power);
              float abs_c = length(pos);
              float m_n = abs(abs_last_z - abs_c);
              float M_n = abs_last_z + abs_c;
              sum += (length(z) - m_n) / (M_n - m_n);
            }
          }

          else if(coloring == 5){
            sum3 = sum2;
            sum2 = sum1;
            sum1 = sum;

            if(iter > 1.0){
              float arg = atan(z.y / z.x);
              sum += 0.5 * sin(2.0 * arg) + 0.5;
            }
          }

          if(z.x * z.x + z.y * z.y > max_abs_sq){
            completed_iterations = iter;
            if(coloring == 3){
              color = iter / max_iter;
            }
            else if(coloring == 2){
              color = (iter + 1.0 - log(log(length(z))) / log(2.0)) / max_iter;
            }

            hasEscaped = true;
            break;
          }
        }

        if(isAverageColoring){
          sum /= completed_iterations;
          if(completed_iterations > 1.0)
            sum1 /= completed_iterations - 1.0;
          if(completed_iterations > 2.0)
            sum2 /= completed_iterations - 2.0;
          if(completed_iterations > 3.0)
            sum3 /= completed_iterations - 3.0;

          float lp = log(log(max_abs_sq) / 2.0);
          float il = 1.0 / log(2.0);

          float d = il*(lp - log(log(length(z)))) + 1.0;
          color = catmullRom(sum, sum1, sum2, sum3, d);
        }

        color *= color_density;
        color += color_offset;
        color -=  floor(color);

        gl_FragColor = vec4(hsv2rgb(vec3(color, 0.85, 0.85)), 1.0);

        if(!hasEscaped){
          gl_FragColor = vec4(0, 0, 0, 1);
        }

      //  gl_FragColor = vec4(1.0, 0, 0, 1);
      }
    </script>

    <script src = "../lib/three.js/three.min.js"></script>
    <script src = "../lib/three.js/stats.min.js"></script>
    <script src = "../lib/dat.gui.min.js"></script>
    <script src = "mandelbrot.js"></script>
    <script src = "controls.js"></script>
    <script src = "../lib/Utils.js"></script>

    <div id = "bellowAbout">
    </div>
  </body>
</html>
